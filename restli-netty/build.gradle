apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  implementation externalDependency.netty
  shadow externalDependency.slf4jApi
}

tasks.named('shadowJar') {
  archiveClassifier.set('')

  dependencies {
    // Don't use externalDependency, since it would only exclude a specific SLF4J API version.
    exclude dependency('org.slf4j:slf4j-api')
  }

  exclude 'META-INF/io.netty.versions.properties'
  exclude 'META-INF/maven/'
  exclude 'META-INF/native-image/'

  relocate 'io.netty', 'com.linkedin.pegasus.io.netty'
  relocate 'META-INF/native/libnetty', 'META-INF/native/libcom_linkedin_pegasus_netty'
  relocate 'META-INF/native/netty', 'META-INF/native/com_linkedin_pegasus_netty'

  mergeServiceFiles()
}

tasks.named('jar') {
  enabled = false

  // Workaround for IDEA-163411.
  // Even when disabled, IntelliJ interprets the regular jar task as producing the unclassified jar. Although the build
  // works with Gradle, IntelliJ will incorrectly fail to find the shaded classes and show spurious errors in the IDE.
  // Giving this a specific classifier prevents IntelliJ's confusion.
  archiveClassifier.set('slim')
}
