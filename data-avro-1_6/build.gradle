/**
 * Use a different module to avoid having the main data module depend on
 * unnecessary external libraries, such as Avro.
 */

dependencies {
  compile project(':data')
  compile project(':data-avro')
  compile externalDependency.jacksonCoreAsl_1_8
  compile externalDependency.avro_1_6
  compile externalDependency.avroUtil
  testCompile externalDependency.testng
  testCompile project(path: ':data-avro', configuration: 'testArtifacts')
  testCompile project(path: ':data-avro-generator', configuration: 'testArtifacts')
  testCompile project(path: ':data', configuration: 'testArtifacts')
  testRuntime project(path: ':data-avro-generator')
}

idea.module.iml.whenMerged { module ->
  def jarDeps = module.dependencies.findAll {
    it instanceof org.gradle.plugins.ide.idea.model.ModuleLibrary
  }
  module.dependencies = jarDeps + module.dependencies
}

sourceSets.test.resources {
  final dataAvroProject = project.evaluationDependsOn(':data-avro')
  it.source(dataAvroProject.sourceSets.test.resources)
}

compileJava.options.compilerArgs += '-Xlint:-deprecation'
compileTestJava.options.compilerArgs += '-Xlint:-deprecation'