/*
   Copyright (c) 2012 LinkedIn Corp.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

final Configuration restliToolsConfig = project(':restli-tools').configurations.default

project.sourceSets.all { SourceSet sourceSet ->
  final inputIdlDirPath = "src${File.separatorChar}${sourceSet.name}${File.separatorChar}idl"
  final FileTree inputIdlFiles = project.fileTree(dir: inputIdlDirPath, includes: ["**${File.separatorChar}*.restspec.json".toString()])
  if (inputIdlFiles.empty)
  {
    return;
  }

  final String outputDirPath = rootProject.ext.build.getRestClientOutDirPath(sourceSet)
  sourceSet.java.srcDir(outputDirPath)

  final Task dataTemplateCompileTask = rootProject.ext.build.dataTemplateCompileTasks[sourceSet];
  rootProject.ext.build.restClientGenerateTasks[sourceSet] = project.task(sourceSet.name + 'GenerateRestClient', type: JavaExec) {
    main = 'com.linkedin.restli.tools.clientgen.RestRequestBuilderGenerator'
    classpath = restliToolsConfig + restliToolsConfig.allArtifacts.files
    args outputDirPath
    args inputIdlFiles
    systemProperties(['generator.resolver.path': "src${File.separatorChar}${sourceSet.name}${File.separatorChar}pegasus:" + dataTemplateCompileTask.destinationDir.path,
                      'generator.rest.generate.datatemplates': 'false'])
    doFirst {
      project.mkdir(outputDirPath)
    }
  }

  // compile generated request builder classes in a separate task before the regular Java classes are compiled
  final Task restClientCompileTask = project.task(sourceSet.name + 'CompileRestClient',
                                                  type: JavaCompile,
                                                  dependsOn: rootProject.ext.build.restClientGenerateTasks[sourceSet]) {
    classpath = sourceSet.compileClasspath + project.files(dataTemplateCompileTask.destinationDir)
    destinationDir = project.file(project.tasks[sourceSet.compileJavaTaskName].destinationDir.path + 'RestClient')
    source outputDirPath
  }
  rootProject.ext.build.restClientCompileTasks[sourceSet] = restClientCompileTask

  project.tasks[sourceSet.compileJavaTaskName].dependsOn(restClientCompileTask)
}
