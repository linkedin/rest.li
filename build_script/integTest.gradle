/**
 * The purpose of this script is to configure the following tasks for a sub-project:
 * - integTest: Integration tests "to verify the correctness of a plugin end-to-end"
 *   - Used to create functionality tests for a gradle plugin
 *   - Used to create cacheability tests for a gradle plugin
 *
 * NOTE: If you apply this script, then in the applying module you will not need to...
 *   1) apply plugin: 'java'
 *   2) dependencies { implementation gradleApi() }
 */

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

sourceSets {
  integTest {
    groovy.srcDir file('src/integTest/groovy')
    resources.srcDir file('src/integTest/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntime
    runtimeClasspath += output + compileClasspath
  }
}

dependencies {
  integTestImplementation externalDependency.spock
}

task integTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integTest
